plugins {
  alias(libs.plugins.starter.application.android)
  alias(libs.plugins.app.cash.paparazzi)
}

android {
  namespace = "io.github.usefulness.shimmer.sample"
  defaultConfig {
    minSdk = 28
    applicationId = "io.github.usefulness.shimmer.sample"
  }
  buildFeatures {
    viewBinding = true
  }
  signingConfigs {
    named("debug") {
      storeFile = rootProject.file("keys/debug.keystore")
      storePassword = 'android'
      keyAlias = 'androiddebugkey'
      keyPassword = 'android'
    }
  }
  buildTypes {
    named("debug") {
      signingConfig = signingConfigs.getByName("debug")
      applicationIdSuffix ".debug"
      versionNameSuffix "-debug"
    }
    named("release") {
      signingConfig = signingConfigs.getByName("debug")
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      minifyEnabled = true
    }
  }

  lint {
    disable("SetTextI18n")
    disable("HardcodedText")
  }
}

afterEvaluate {
  dependencies.constraints {
    add("testImplementation", "com.google.guava:guava") {
      attributes {
        attribute(
          TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE,
          objects.named(TargetJvmEnvironment, TargetJvmEnvironment.STANDARD_JVM)
        )
      }
      because("LayoutLib and sdk-common depend on Guava's -jre published variant." +
        "See https://github.com/cashapp/paparazzi/issues/906.")
    }
  }
}

dependencies {
  implementation("io.github.usefulness:shimmer-android-core")
  implementation(libs.appcompat.core)
  implementation(libs.constraintlayout.core)
  implementation(libs.material.core)
  implementation(libs.androidx.core)
  implementation(libs.androidx.lifecycle.viewmodel)
  implementation(libs.androidx.activity.core)
  implementation(libs.androidx.fragment.core)
  debugImplementation(libs.leakcanary.android)

  testImplementation(libs.junit.jupiter)
}
