plugins {
  alias(libs.plugins.starter.library.android)
  id("com.starter.publishing")
  alias(libs.plugins.kotlinx.binarycompatibility)
}
android {
  namespace = "io.github.usefulness.shimmer.android"
  defaultConfig {
    minSdk 23
  }
  publishing {
    singleVariant("release")
  }
  lint {
    disable("UnknownNullness")
  }
}

kotlin {
  explicitApi()
}

androidComponents {
  beforeVariants(selector().withBuildType("release")) { builder ->
    builder.enable = true
  }
}

tasks.withType(JavaCompile).configureEach {
  options.release.set(JavaVersion.VERSION_1_8.majorVersion.toInteger())
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  testLogging {
    events "skipped", "failed"
  }
}


dependencies {
  implementation(libs.androidx.annotation)

  testImplementation(libs.junit.jupiter)
  testImplementation(libs.assertj.core)
}
